#include "imports/stdlib.fc";
#include "imports/constants.fc";
#include "imports/errors.fc";
#include "imports/events.fc";
#include "imports/messages.fc";
#include "imports/op-codes.fc";
#include "imports/vesting-utils.fc";
#include "imports/params.fc";

;; storage variables

global int storage::current_period;
global int storage::latest_time;
global int storage::balance;
global slice storage::operator_address;
global slice storage::owner_address;
global cell storage::vesting_item_code;
global cell storage::token_info;

;; load_data populates storage variables using stored data
() load_data() impure {
    var ds = get_data().begin_parse();

    storage::current_period = ds~load_uint(8);
    storage::latest_time = ds~load_uint(32);
    storage::balance = ds~load_coins();
    storage::operator_address = ds~load_msg_addr();
    storage::owner_address = ds~load_msg_addr();
    storage::vesting_item_code = ds~load_ref();
    storage::token_info = ds~load_ref();
    
    ds.end_parse();
}

;; save_data stores storage variables as a cell into persistent storage
() save_data() impure {
    set_data(
        begin_cell()
            .store_uint(storage::current_period, 8)
            .store_uint(storage::latest_time, 32)
            .store_coins(storage::balance)
            .store_slice(storage::operator_address)
            .store_slice(storage::owner_address)
            .store_ref(storage::vesting_item_code)
            .store_ref(storage::token_info)
            .end_cell()
    );
}

(slice, cell) decode(cell token_info) inline {
    var cs = token_info.begin_parse();
    return (cs~load_msg_addr(), cs~load_ref());
}

() transfer_jetton(
    int query_id,
    int jetton_amount,
    slice to_address,
    slice token_address,
    cell jetton_wallet_code
) impure inline {
    slice jetton_wallet_address = calculate_user_jetton_wallet_address(
        my_address(),
        token_address,
        jetton_wallet_code
    );
    cell internal_body = begin_cell()
                        .store_uint(op::transfer(), 32)
                        .store_uint(query_id, 64)
                        .store_coins(jetton_amount)
                        .store_slice(to_address)
                        .store_slice(to_address)
                        .store_int(0, 1)
                        .store_coins(0)
                        .store_int(0, 1)
                        .end_cell();
    var body = begin_cell()
                    .store_uint(0x18, 6) 
                    .store_slice(jetton_wallet_address)
                    .store_coins(0)
                    .store_uint(1, 1 + 4 + 4 + 64 + 32 + 1 + 1)
                    .store_ref(internal_body);
    send_raw_message(body.end_cell(), 64);
}

;; recv_internal is the main function of the contract and is called when it receives a message from other contracts
() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if (in_msg_body.slice_empty?()) { ;; ignore all empty messages
        return ();
    }

    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);
    if (flags & 1) { ;; ignore all bounced messages
        return ();
    }

    load_data(); ;; here we populate the storage variables

    slice sender_address = cs~load_msg_addr();
    cs~load_msg_addr(); ;; skip dst
    cs~load_coins(); ;; skip value
    cs~skip_bits(1); ;; skip extracurrency collection
    cs~load_coins(); ;; skip ihr_fee
    int fwd_fee = cs~load_coins(); ;; we use message fwd_f

    int op = in_msg_body~load_uint(32); ;; by convention, the first 32 bits of incoming message is the op
    int query_id = in_msg_body~load_uint(64); ;; also by convention, the next 64 bits contain the "query id", although this is not always the case

    if (op == op::transfer_notification()) {
        int jetton_amount = in_msg_body~load_coins();
        throw_unless(error::INVALID_AMOUNT(), jetton_amount > 0);
        (slice token_address, cell jetton_wallet_code) = decode(storage::token_info);
        slice wallet_address = calculate_user_jetton_wallet_address(
            my_address(),
            token_address,
            jetton_wallet_code
        );
        throw_unless(error::INVALID_CALLER(), equal_slices_bits(sender_address, wallet_address));
        storage::balance += jetton_amount;
        save_data();
        return();
    }

    if (op == op::create_vesting()) {
        throw_unless(error::NOT_OWNER(), equal_slices_bits(sender_address, storage::owner_address));
        throw_unless(error::INVALID_MIN_TON(), (my_balance >= const::min_tons_for_storage()));  
        throw_unless(error::INVALID_VALUE(), (msg_value >= 2 * fwd_fee + 5 * const::provide_address_gas_consumption()));
        (
            int total_vesting,
            slice owner_item
        ) = (
            in_msg_body~load_coins(),
            in_msg_body~load_msg_addr()
        );
        (
            slice reward_token_address,
            cell jetton_wallet_code
        ) = decode(storage::token_info);
        cell item_state_init = calculate_item_state_init(
            my_address(),
            owner_item,
            reward_token_address,
            jetton_wallet_code,
            storage::vesting_item_code
        );
        slice item_address = calculate_item_address(item_state_init);
        cell internal_body = begin_cell()
                            .store_uint(op::internal_create(), 32)
                            .store_uint(query_id, 64)
                            .store_coins(total_vesting)
                            .store_slice(owner_item)
                            .end_cell();
        var body = begin_cell()
                .store_uint(op::initialize(), 32)
                .store_uint(query_id, 64)
                .store_coins(total_vesting)
                .store_ref(internal_body);
        send_message_with_stateinit(2 * fwd_fee + 5 * const::provide_address_gas_consumption(), item_address, item_state_init, body.end_cell(), NORMAL);
        return();
    }

    if (op == op::internal_create()) {
        (
            int total_vesting,
            slice owner_item
        ) = (
            in_msg_body~load_coins(),
            in_msg_body~load_msg_addr()
        );
        (
            slice reward_token_address,
            cell jetton_wallet_code
        ) = decode(storage::token_info);
        cell item_state_init = calculate_item_state_init(
            my_address(),
            owner_item,
            reward_token_address,
            jetton_wallet_code,
            storage::vesting_item_code
        );
        slice item_address = calculate_item_address(item_state_init);
        throw_unless(error::INVALID_CALLER(), equal_slices_bits(sender_address, item_address));

        emit_log_simple(
            event::created_vesting(),
            begin_cell()
                .store_slice(owner_item)
                .store_coins(total_vesting)
                .end_cell(),
            1
        );
        return();
    }

    if (op == op::claim()) {
        throw_unless(error::INVALID_MIN_TON(), (my_balance >= const::min_tons_for_storage()));  
        throw_unless(error::INVALID_VALUE(), (msg_value >= 6 * fwd_fee + 2 * const::provide_address_gas_consumption()));
        throw_unless(error::TOO_EARLY(), (storage::current_period > 0));
        (
            slice reward_token_address,
            cell jetton_wallet_code
        ) = decode(storage::token_info);
        cell item_state_init = calculate_item_state_init(
            my_address(),
            sender_address,
            reward_token_address,
            jetton_wallet_code,
            storage::vesting_item_code
        );
        slice item_address = calculate_item_address(item_state_init);
        var body = begin_cell()
                .store_uint(op::claim(), 32)
                .store_uint(query_id, 64)
                .store_uint(storage::current_period, 8);
        send_simple_message(0, item_address, body.end_cell(), CARRY_REMAINING_GAS);
        return();
    }

    if (op == op::internal_claim()) {
        (
            int claimable,
            slice owner_item,
            slice reward_token_address,
            cell jetton_wallet_code
        ) = (
            in_msg_body~load_coins(),
            in_msg_body~load_msg_addr(),
            in_msg_body~load_msg_addr(),
            in_msg_body~load_ref()
        );
        throw_unless(error::EXCEED_BALANCE(), (claimable <= storage::balance));
        cell item_state_init = calculate_item_state_init(
            my_address(),
            owner_item,
            reward_token_address,
            jetton_wallet_code,
            storage::vesting_item_code
        );
        slice item_address = calculate_item_address(item_state_init);
        throw_unless(error::INVALID_CALLER(), equal_slices_bits(sender_address, item_address));
        transfer_jetton(
            query_id,
            claimable,
            owner_item,
            reward_token_address,
            jetton_wallet_code
        );
        storage::balance -= claimable;
        save_data();
        emit_log_simple(
            event::claimed(),
            begin_cell()
                .store_slice(owner_item)
                .store_coins(claimable)
                .end_cell(),
            1
        );
        return();
    }

   
    if (op == op::increase()) {
        throw_unless(error::NOT_OPERATOR(), equal_slices_bits(sender_address, storage::operator_address));
        throw_unless(error::INVALID_MIN_TON(), (my_balance >= const::min_tons_for_storage()));  
        throw_unless(error::MAX_PERIOD(), (storage::current_period < 12));
        throw_unless(error::TOO_EARLY(), (now() >= storage::latest_time + const::min_period()));
        storage::current_period += 1;
        storage::latest_time = now();
        save_data();
        emit_log_simple(
            event::increased_period(),
            begin_cell()
                .store_uint(storage::current_period, 8)
                .end_cell(),
            1
        );
        return ();
    }

    if (op == op::change_owner()) {
        throw_unless(error::NOT_OWNER(), equal_slices_bits(sender_address, storage::owner_address));
        slice new_owner_address = in_msg_body~load_msg_addr();
        force_chain(new_owner_address);
        storage::owner_address = new_owner_address;
        save_data();
        return ();
    }

    if (op == op::change_operator()) {
        throw_unless(error::NOT_OWNER(), equal_slices_bits(sender_address, storage::owner_address));
        slice new_operator = in_msg_body~load_msg_addr();
        force_chain(new_operator);
        storage::operator_address = new_operator;
        save_data();
        return ();
    }

    if (op == op::owner_withdraw()) {
        throw_unless(error::NOT_OWNER(), equal_slices_bits(sender_address, storage::owner_address));
        int amount_ton = in_msg_body~load_coins();
        slice to_address = in_msg_body~load_msg_addr();
        force_chain(to_address);
        throw_unless(error::INVALID_MIN_TON(), (my_balance >= (amount_ton + const::min_tons_for_storage())));  
        var body = begin_cell()
                    .store_uint(0x10, 6)
                    .store_slice(to_address)
                    .store_coins(amount_ton)
                    .store_uint(0, 107)
                    .end_cell();
        send_raw_message(body, 1);
        return ();
    }

    if (op == op::owner_withdraw_jetton()) {
        throw_unless(error::NOT_OWNER(), equal_slices_bits(sender_address, storage::owner_address));
        (int amount, slice to_address, slice token_address, cell jetton_wallet_code) = (in_msg_body~load_coins(), in_msg_body~load_msg_addr(), in_msg_body~load_msg_addr(), in_msg_body~load_ref());
        force_chain(to_address);
        transfer_jetton(
            query_id,
            amount,
            to_address,
            token_address,
            jetton_wallet_code
        );
        return ();
    }

    throw(0xffff); ;; if the message contains an op that is not known to this contract, we throw
}

;; get methods are a means to conveniently read contract data using, for example, HTTP APIs
;; they are marked with method_id
;; note that unlike in many other smart contract VMs, get methods cannot be called by other contracts

(int, int, int, slice, slice, cell, cell) get_vesting_data() method_id {
    load_data();
    return (
        storage::current_period,
        storage::latest_time,
        storage::balance,
        storage::operator_address,
        storage::owner_address,
        storage::vesting_item_code,
        storage::token_info
    );
}

(slice, cell) get_token_data() method_id {
    load_data();
    return decode(storage::token_info);
}

slice get_item_address(
    slice owner_item
) method_id {
    load_data();
    (
        slice reward_token_address,
        cell jetton_wallet_code
    ) = decode(storage::token_info);
    return calculate_user_item_address(
        my_address(),
        owner_item,
        reward_token_address,
        jetton_wallet_code,
        storage::vesting_item_code
    );
}